#!/usr/bin/perl

use v5.30;

use Getopt::Long;
use DBI;
use JSON::XS;

use Data::Dumper;

Getopt::Long::Configure('gnu_getopt');

my $host   = 'localhost';
my $port   = 5432;
my $dbname = 'daq';
my $number = 1;
GetOptions(
  'host|h=s'     => \$host,
  'port|p=i'     => \$port,
  'database|d=s' => \$dbname,
  'n=i',         => \$number
) or die;

my $db = DBI->connect(
  "dbi:Pg:dbname=$dbname;host=$host;port=$port",
  undef,
  undef,
  { RaiseError => 1 }
) or exit 1;

my $event_number = $db->selectall_arrayref(
  'select max(evnt) from event_display'
)->[0][0];
if (defined $event_number) {
  ++$event_number;
} else {
  $event_number = 0;
};

my $json = JSON::XS->new->canonical;

my $pmts = $db->selectall_arrayref('select id from pmt');

my @event = map { { pmt => $_->[0] } } @$pmts;
$#event = $#{$pmts};

my $q = $db->prepare(<<~'END');
  insert into event_display (evnt, time, data) values ($1, now(), $2)
END

while ($number-- > 0) {
  for (@event) {
    $_->{c} = 10 * rand();
    $_->{t} = 100 + rand() * 150;
  };
  $q->execute($event_number++, $json->encode(\@event));
};
